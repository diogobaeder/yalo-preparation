export NATS_URL := "nats://localhost:4222,nats://localhost:4223,nats://localhost:4224"
export SCYLLA_ADDRS := "localhost:9042,localhost:9043,localhost:9044"
export SCYLLA_MIGRATION_URL := "cassandra://localhost:9042"
export SCYLLA_KEYSPACE := "yalo"
export MIGRATIONS_PATH := "db/migrations"

KEYSPACES := "yalo tests"

test-all:
    export SCYLLA_KEYSPACE="tests"
    go test -v ./...

run-bot:
    go run cmd/bot/bot.go

run-auditor:
    go run cmd/auditor/auditor.go

create-scylla-keyspaces:
    #!/usr/bin/env sh
    for keyspace in {{ KEYSPACES }}; do
        echo "Ensuring keyspace ${keyspace} is created"
        cqlsh -e "CREATE KEYSPACE IF NOT EXISTS ${keyspace} WITH REPLICATION = {'class': 'SimpleStrategy', 'replication_factor': 1}" localhost 9042
    done

create-scylla-migration:
    #!/usr/bin/env sh
    echo "Type the file-friendly name for the migration:"
    read name
    migrate create -ext cql -dir ${MIGRATIONS_PATH} -seq -digits 4 ${name}

migrate-up:
    #!/usr/bin/env sh
    echo "Migrating ScyllaDB up"
    for keyspace in {{ KEYSPACES }}; do
        migrate -database ${SCYLLA_MIGRATION_URL}/${keyspace} -path ${MIGRATIONS_PATH} up
    done

migrate-down:
    #!/usr/bin/env sh
    echo "Migrating ScyllaDB down"
    for keyspace in {{ KEYSPACES }}; do
        migrate -database ${SCYLLA_MIGRATION_URL}/${keyspace} -path ${MIGRATIONS_PATH} down
    done

send-requests:
    #!/usr/bin/env sh
    nats pub --count=1000 --sleep='5ms' 'yalo.request.johndoe' 'Heres a counter: {{'{{ Count }}'}}'